# Adapted from the blog post:
# https://devblogs.microsoft.com/cppblog/clear-functional-c-documentation-with-sphinx-breathe-doxygen-cmake/

find_package(Doxygen REQUIRED)
find_package(Sphinx REQUIRED)

set(DOXYGEN_OUTPUT_DIRECTORY "${CMAKE_CURRENT_BINARY_DIR}/doxygen")
set(DOXYGEN_INDEX_FILE "${DOXYGEN_OUTPUT_DIRECTORY}/xml/index.xml")
set(DOXYFILE_IN ${CMAKE_CURRENT_SOURCE_DIR}/Doxyfile.in)
set(DOXYFILE_OUT ${CMAKE_CURRENT_BINARY_DIR}/Doxyfile)
file(GLOB_RECURSE ENHANCEDENUM_HEADERS "${ENHANCEDENUM_INCLUDE_DIRS}/*.hh")
configure_file(${DOXYFILE_IN} ${DOXYFILE_OUT} @ONLY)
add_custom_command(OUTPUT ${DOXYGEN_INDEX_FILE}
  COMMAND Doxygen::doxygen ${DOXYFILE_OUT}
  MAIN_DEPENDENCY ${DOXYFILE_IN}
  DEPENDS ${ENHANCEDENUM_HEADERS}
  COMMENT "Generating Doxygen docs")

set(SPHINX_SOURCE_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR})
set(SPHINX_BUILD "${CMAKE_CURRENT_BINARY_DIR}/sphinx")
set(SPHINX_INDEX_FILE "${SPHINX_BUILD}/index.html")
set(ENHANCEDENUM_SPHINX_CONFIG "${SPHINX_SOURCE_DIRECTORY}/conf.py")
file(GLOB_RECURSE ENHANCEDENUM_RSTS "${SPHINX_SOURCE_DIRECTORY}/*.rst")
file(GLOB_RECURSE ENHANCEDENUM_ENUMECG_PYS "${SPHINX_SOURCE_DIRECTORY}/../python/enumecg/*.py")
add_custom_command(OUTPUT ${SPHINX_INDEX_FILE}
  COMMAND ${SPHINX_EXECUTABLE} -b html
    "-Dbreathe_projects.EnhancedEnum=${DOXYGEN_OUTPUT_DIRECTORY}/xml"
    ${SPHINX_SOURCE_DIRECTORY} ${SPHINX_BUILD}
  MAIN_DEPENDENCY ${ENHANCEDENUM_SPHINX_CONFIG}
  DEPENDS ${DOXYGEN_INDEX_FILE} ${ENHANCEDENUM_RSTS} ${ENHANCEDENUM_ENUMECG_PYS}
  COMMENT "Generating Sphinx documentation")

add_custom_target(docs ALL DEPENDS ${SPHINX_INDEX_FILE} ${DOXYGEN_INDEX_FILE})
